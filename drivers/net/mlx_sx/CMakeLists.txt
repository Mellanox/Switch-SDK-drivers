
set (TARGET_NAME sx_core)
set (MODULE_NAME ${TARGET_NAME}.ko)
set (TARGET_NAME_CLEAN ${TARGET_NAME}_clean)
file(RELATIVE_PATH SXD_MODULE_PATH ${SXD_KERNEL_PATH} ${CMAKE_CURRENT_SOURCE_DIR})
set (MODULE_CMD ${MODULE_CMD_COMMON} EXTRA_SYMVERS=${MEMTRACK_EXTRA_SYMVERS_VAR} M=${CMAKE_CURRENT_BINARY_DIR})

file(GLOB CSRCS CONFIGURE_DEPENDS "${CMAKE_CURRENT_SOURCE_DIR}/*.c")
file(GLOB HSRCS CONFIGURE_DEPENDS "${CMAKE_CURRENT_SOURCE_DIR}/*.h")
file(GLOB AUTOSRCS CONFIGURE_DEPENDS "${CMAKE_CURRENT_SOURCE_DIR}/auto_registers/*.c")

set(SRCS ${CSRCS} ${HSRCS} ${AUTOSRCS} ${CMAKE_CURRENT_SOURCE_DIR}/Makefile)

add_custom_command (
    OUTPUT ${MODULE_NAME}
    COMMAND ${MODULE_CMD} modules
    COMMENT "Building ${MODULE_NAME}"
    DEPENDS ${SRCS} ${INCLUDE_HEADERS_ALL} ${COPY_SXD_KERNEL_TARGET}
    VERBATIM
)

add_custom_target (
    ${TARGET_NAME} ALL DEPENDS ${MODULE_NAME}
)

if (MEMTRACK)
    add_dependencies(${TARGET_NAME} memtrack)
endif()

add_custom_target (${TARGET_NAME_CLEAN}
    COMMAND ${MODULE_CMD} clean
    COMMENT "Cleaning ${MODULE_NAME}"
)

install (
    PROGRAMS ${CMAKE_CURRENT_BINARY_DIR}/${MODULE_NAME}
    DESTINATION lib/modules/${OS_RELEASE}/update/kernel/${SXD_MODULE_PATH}
)

add_subdirectory(debug)
